buildscript {
	repositories {
		mavenCentral()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
		maven {
			name = "sonatype"
			url = "https://oss.sonatype.org/content/repositories/snapshots/"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
	}
}

apply plugin: 'forge'
apply plugin: 'maven'

version = "$minecraft_version-$mod_version"

if (System.getenv().DRONE_BUILD_NUMBER != null) {
	version += "-snapshot-" + System.getenv().DRONE_BUILD_NUMBER
}

minecraft {
	version = "$minecraft_version-$forge_version"
	runDir = "run"

	replaceIn "automod/AutoMod.java"
	replace '@VERSION@', "$mod_version"
}

repositories {
	maven {
		name = "shadowfacts"
		url = "http://maven.shadowfacts.net"
	}
}

configurations {
	shade
	compile.extendsFrom shade
}

dependencies {
	shade group: 'net.shadowfacts', name: 'ShadowAPI', version: '1.7.10-2.2.0'
}

processResources
{
	inputs.property "version", "$mod_version"

	from (sourceSets.main.resources.srcDirs) {
		include '**/*.info'
		expand 'version': "$mod_version", 'mcversion': "$minecraft_version"
	}

	from (sourceSets.main.resources.srcDirs) {
		exclude '**/*.info'
	}
}

jar {
	configurations.shade.each { dep ->
		from (project.zipTree(dep)) {
			exclude 'META-INF', 'META-INF/**'
		}
	}
}

task deobfJar(type: Jar) {
	from sourceSets.main.output
	configurations.shade.each { dep ->
		from (project.zipTree(dep)) {
			exclude 'META-INF', 'META-INF/**'
		}
	}
	classifier = 'deobf'
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	configurations.shade.each { dep ->
		from (project.zipTree(dep)) {
			exclude 'META-INF', 'META-INF/**'
		}
	}
	classifier = 'sources'
}

tasks.build.dependsOn deobfJar, sourcesJar

artifacts {
	archives jar
	archives deobfJar
	archives sourcesJar
}

uploadArchives {
	repositories.mavenDeployer {
		repository(url: "file:///maven/")
	}
}

// IDEA bug
idea { module { inheritOutputDirs = true } }